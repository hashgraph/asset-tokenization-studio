/*
                                 Apache License
                           Version 2.0, January 2004
                        http://www.apache.org/licenses/

   TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION

   1. Definitions.

      "License" shall mean the terms and conditions for use, reproduction,
      and distribution as defined by Sections 1 through 9 of this document.

      "Licensor" shall mean the copyright owner or entity authorized by
      the copyright owner that is granting the License.

      "Legal Entity" shall mean the union of the acting entity and all
      other entities that control, are controlled by, or are under common
      control with that entity. For the purposes of this definition,
      "control" means (i) the power, direct or indirect, to cause the
      direction or management of such entity, whether by contract or
      otherwise, or (ii) ownership of fifty percent (50%) or more of the
      outstanding shares, or (iii) beneficial ownership of such entity.

      "You" (or "Your") shall mean an individual or Legal Entity
      exercising permissions granted by this License.

      "Source" form shall mean the preferred form for making modifications,
      including but not limited to software source code, documentation
      source, and configuration files.

      "Object" form shall mean any form resulting from mechanical
      transformation or translation of a Source form, including but
      not limited to compiled object code, generated documentation,
      and conversions to other media types.

      "Work" shall mean the work of authorship, whether in Source or
      Object form, made available under the License, as indicated by a
      copyright notice that is included in or attached to the work
      (an example is provided in the Appendix below).

      "Derivative Works" shall mean any work, whether in Source or Object
      form, that is based on (or derived from) the Work and for which the
      editorial revisions, annotations, elaborations, or other modifications
      represent, as a whole, an original work of authorship. For the purposes
      of this License, Derivative Works shall not include works that remain
      separable from, or merely link (or bind by name) to the interfaces of,
      the Work and Derivative Works thereof.

      "Contribution" shall mean any work of authorship, including
      the original version of the Work and any modifications or additions
      to that Work or Derivative Works thereof, that is intentionally
      submitted to Licensor for inclusion in the Work by the copyright owner
      or by an individual or Legal Entity authorized to submit on behalf of
      the copyright owner. For the purposes of this definition, "submitted"
      means any form of electronic, verbal, or written communication sent
      to the Licensor or its representatives, including but not limited to
      communication on electronic mailing lists, source code control systems,
      and issue tracking systems that are managed by, or on behalf of, the
      Licensor for the purpose of discussing and improving the Work, but
      excluding communication that is conspicuously marked or otherwise
      designated in writing by the copyright owner as "Not a Contribution."

      "Contributor" shall mean Licensor and any individual or Legal Entity
      on behalf of whom a Contribution has been received by Licensor and
      subsequently incorporated within the Work.

   2. Grant of Copyright License. Subject to the terms and conditions of
      this License, each Contributor hereby grants to You a perpetual,
      worldwide, non-exclusive, no-charge, royalty-free, irrevocable
      copyright license to reproduce, prepare Derivative Works of,
      publicly display, publicly perform, sublicense, and distribute the
      Work and such Derivative Works in Source or Object form.

   3. Grant of Patent License. Subject to the terms and conditions of
      this License, each Contributor hereby grants to You a perpetual,
      worldwide, non-exclusive, no-charge, royalty-free, irrevocable
      (except as stated in this section) patent license to make, have made,
      use, offer to sell, sell, import, and otherwise transfer the Work,
      where such license applies only to those patent claims licensable
      by such Contributor that are necessarily infringed by their
      Contribution(s) alone or by combination of their Contribution(s)
      with the Work to which such Contribution(s) was submitted. If You
      institute patent litigation against any entity (including a
      cross-claim or counterclaim in a lawsuit) alleging that the Work
      or a Contribution incorporated within the Work constitutes direct
      or contributory patent infringement, then any patent licenses
      granted to You under this License for that Work shall terminate
      as of the date such litigation is filed.

   4. Redistribution. You may reproduce and distribute copies of the
      Work or Derivative Works thereof in any medium, with or without
      modifications, and in Source or Object form, provided that You
      meet the following conditions:

      (a) You must give any other recipients of the Work or
          Derivative Works a copy of this License; and

      (b) You must cause any modified files to carry prominent notices
          stating that You changed the files; and

      (c) You must retain, in the Source form of any Derivative Works
          that You distribute, all copyright, patent, trademark, and
          attribution notices from the Source form of the Work,
          excluding those notices that do not pertain to any part of
          the Derivative Works; and

      (d) If the Work includes a "NOTICE" text file as part of its
          distribution, then any Derivative Works that You distribute must
          include a readable copy of the attribution notices contained
          within such NOTICE file, excluding those notices that do not
          pertain to any part of the Derivative Works, in at least one
          of the following places: within a NOTICE text file distributed
          as part of the Derivative Works; within the Source form or
          documentation, if provided along with the Derivative Works; or,
          within a display generated by the Derivative Works, if and
          wherever such third-party notices normally appear. The contents
          of the NOTICE file are for informational purposes only and
          do not modify the License. You may add Your own attribution
          notices within Derivative Works that You distribute, alongside
          or as an addendum to the NOTICE text from the Work, provided
          that such additional attribution notices cannot be construed
          as modifying the License.

      You may add Your own copyright statement to Your modifications and
      may provide additional or different license terms and conditions
      for use, reproduction, or distribution of Your modifications, or
      for any such Derivative Works as a whole, provided Your use,
      reproduction, and distribution of the Work otherwise complies with
      the conditions stated in this License.

   5. Submission of Contributions. Unless You explicitly state otherwise,
      any Contribution intentionally submitted for inclusion in the Work
      by You to the Licensor shall be under the terms and conditions of
      this License, without any additional terms or conditions.
      Notwithstanding the above, nothing herein shall supersede or modify
      the terms of any separate license agreement you may have executed
      with Licensor regarding such Contributions.

   6. Trademarks. This License does not grant permission to use the trade
      names, trademarks, service marks, or product names of the Licensor,
      except as required for reasonable and customary use in describing the
      origin of the Work and reproducing the content of the NOTICE file.

   7. Disclaimer of Warranty. Unless required by applicable law or
      agreed to in writing, Licensor provides the Work (and each
      Contributor provides its Contributions) on an "AS IS" BASIS,
      WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
      implied, including, without limitation, any warranties or conditions
      of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
      PARTICULAR PURPOSE. You are solely responsible for determining the
      appropriateness of using or redistributing the Work and assume any
      risks associated with Your exercise of permissions under this License.

   8. Limitation of Liability. In no event and under no legal theory,
      whether in tort (including negligence), contract, or otherwise,
      unless required by applicable law (such as deliberate and grossly
      negligent acts) or agreed to in writing, shall any Contributor be
      liable to You for damages, including any direct, indirect, special,
      incidental, or consequential damages of any character arising as a
      result of this License or out of the use or inability to use the
      Work (including but not limited to damages for loss of goodwill,
      work stoppage, computer failure or malfunction, or any and all
      other commercial damages or losses), even if such Contributor
      has been advised of the possibility of such damages.

   9. Accepting Warranty or Additional Liability. While redistributing
      the Work or Derivative Works thereof, You may choose to offer,
      and charge a fee for, acceptance of support, warranty, indemnity,
      or other liability obligations and/or rights consistent with this
      License. However, in accepting such obligations, You may act only
      on Your own behalf and on Your sole responsibility, not on behalf
      of any other Contributor, and only if You agree to indemnify,
      defend, and hold each Contributor harmless for any liability
      incurred by, or claims asserted against, such Contributor by reason
      of your accepting any such warranty or additional liability.

   END OF TERMS AND CONDITIONS

   APPENDIX: How to apply the Apache License to your work.

      To apply the Apache License to your work, attach the following
      boilerplate notice, with the fields enclosed by brackets "[]"
      replaced with your own identifying information. (Don't include
      the brackets!)  The text should be enclosed in the appropriate
      comment syntax for the file format. We also recommend that a
      file or class name and description of purpose be included on the
      same "printed page" as the copyright notice for easier
      identification within third-party archives.

   Copyright [yyyy] [name of copyright owner]

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.

*/

// SPDX-License-Identifier: MIT
pragma solidity 0.8.18;

import {
    ERC1594StorageWrapper
} from '../ERC1400/ERC1594/ERC1594StorageWrapper.sol';
import {IERC3643} from '../interfaces/ERC3643/IERC3643.sol';
import {ICompliance} from '../interfaces/ERC3643/ICompliance.sol';
import {IIdentityRegistry} from '../interfaces/ERC3643/IIdentityRegistry.sol';
import {
    IStaticFunctionSelectors
} from '../../interfaces/resolver/resolverProxy/IStaticFunctionSelectors.sol';
import {_ERC3643_RESOLVER_KEY} from '../constants/resolverKeys.sol';
import {
    _DEFAULT_ADMIN_ROLE,
    _CONTROLLER_ROLE,
    _ISSUER_ROLE,
    _FREEZE_MANAGER_ROLE,
    _AGENT_ROLE
} from '../constants/roles.sol';
import {Strings} from '@openzeppelin/contracts/utils/Strings.sol';
import {IKyc} from '../interfaces/kyc/IKyc.sol';
import {_DEFAULT_PARTITION} from '../../layer_0/constants/values.sol';

contract ERC3643 is IERC3643, ERC1594StorageWrapper, IStaticFunctionSelectors {
    using Strings for uint256;

    address private constant _ONCHAIN_ID = address(0);

    function setAddressFrozen(
        address _userAddress,
        bool _freezStatus
    )
        external
        override
        onlyUnpaused
        onlyRole(_FREEZE_MANAGER_ROLE)
        validateAddress(_userAddress)
    {
        _setAddresFrozen(_userAddress, _freezStatus);
        emit AddressFrozen(_userAddress, _freezStatus, _msgSender());
    }

    /**
     * @notice Sets the name of the token.
     * @dev Can only be called by the token `owner/issuer`.
     */
    function setName(
        string calldata _name
    ) external override onlyUnpaused onlyRole(_DEFAULT_ADMIN_ROLE) {
        ERC20Storage storage erc20Storage = _setName(_name);

        emit UpdatedTokenInformation(
            erc20Storage.name,
            erc20Storage.symbol,
            erc20Storage.decimals,
            _getLatestVersion().toString(),
            _erc3643Storage().onchainID
        );
    }

    /**
     * @notice Sets the symbol of the token.
     * @dev Can only be called by the token `owner/issuer`.
     */
    function setSymbol(
        string calldata _symbol
    ) external override onlyUnpaused onlyRole(_DEFAULT_ADMIN_ROLE) {
        ERC20Storage storage erc20Storage = _setSymbol(_symbol);

        emit UpdatedTokenInformation(
            erc20Storage.name,
            erc20Storage.symbol,
            erc20Storage.decimals,
            _getLatestVersion().toString(),
            _erc3643Storage().onchainID
        );
    }

    /**
     * @notice Sets the onchainID address for the token.
     * @dev Can only be called by the token `owner/issuer`.
     */
    function setOnchainID(
        address _onchainID
    ) external override onlyUnpaused onlyRole(_DEFAULT_ADMIN_ROLE) {
        ERC20Storage storage erc20Storage = _erc20Storage();
        _erc3643Storage().onchainID = _onchainID;

        emit UpdatedTokenInformation(
            erc20Storage.name,
            erc20Storage.symbol,
            erc20Storage.decimals,
            _getLatestVersion().toString(),
            _onchainID
        );
    }

    /**
     * @notice Sets the identity registry contract address.
     * @dev Can only be called by the token `owner/issuer`.
     */
    function setIdentityRegistry(
        address _identityRegistry
    ) external override onlyUnpaused onlyRole(_DEFAULT_ADMIN_ROLE) {
        _erc3643Storage().identityRegistry = _identityRegistry;
        emit IdentityRegistryAdded(_identityRegistry);
    }

    /**
     * @notice Sets the compliance contract address.
     * @dev Can only be called by the token `owner/issuer`.
     */
    function setCompliance(
        address _compliance
    ) external override onlyUnpaused onlyRole(_DEFAULT_ADMIN_ROLE) {
        _erc3643Storage().compliance = _compliance;
        emit ComplianceAdded(_compliance);
    }

    /**
     * @notice Burns a specified amount of tokens from a user address.
     * @dev Can only be called by the token `owner/issuer` or `controller`.
     * @param _userAddress The address from which the tokens will be burned.
     * @param _amount The amount of tokens to burn.
     */
    function burn(
        address _userAddress,
        uint256 _amount
    )
        external
        onlyUnpaused
        onlyClearingDisabled
        onlyListedAllowed(_msgSender())
        onlyListedAllowed(_userAddress)
        onlyWithoutMultiPartition
        onlyUnProtectedPartitionsOrWildCardRole
        onlyValidKycStatus(IKyc.KycStatus.GRANTED, _userAddress)
    {
        _redeemFrom(_userAddress, _amount, '');
    }

    /**
     * @notice Mints a specified amount of tokens to a user address.
     * @dev Can only be called by the token `owner/issuer`.
     * @param _to The address to which the tokens will be minted.
     * @param _amount The amount of tokens to mint.
     */
    function mint(
        address _to,
        uint256 _amount
    )
        external
        onlyUnpaused
        onlyWithinMaxSupply(_amount)
        onlyRole(_ISSUER_ROLE)
        onlyListedAllowed(_to)
        onlyWithoutMultiPartition
        onlyIssuable
        onlyValidKycStatus(IKyc.KycStatus.GRANTED, _to)
    {
        _issue(_to, _amount, '');
    }

    /**
     * @notice Transfers tokens from one address to another.
     * @dev Can only be called by the token `owner/issuer` or `controller`.
     * @param _from The address from which the tokens will be transferred.
     * @param _to The address to which the tokens will be transferred.
     * @param _amount The amount of tokens to transfer.
     */
    function forcedTransfer(
        address _from,
        address _to,
        uint256 _amount
    )
        external
        onlyWithoutMultiPartition
        onlyRole(_CONTROLLER_ROLE)
        onlyControllable
        onlyUnpaused
        onlyClearingDisabled
        onlyListedAllowed(_from)
        onlyListedAllowed(_to)
        onlyUnProtectedPartitionsOrWildCardRole
        onlyValidKycStatus(IKyc.KycStatus.GRANTED, _from)
        onlyValidKycStatus(IKyc.KycStatus.GRANTED, _to)
        returns (bool)
    {
        _controllerTransfer(_from, _to, _amount, '', '');
        return true;
    }

    function freezePartialTokens(
        address _userAddress,
        uint256 _amount
    )
        external
        override
        onlyUnpaused
        onlyRole(_FREEZE_MANAGER_ROLE)
        validateAddress(_userAddress)
        onlyWithoutMultiPartition
    {
        _freezeTokens(_userAddress, _amount);
        emit TokensFrozen(_userAddress, _amount, _DEFAULT_PARTITION);
    }

    function unfreezePartialTokens(
        address _userAddress,
        uint256 _amount
    )
        external
        override
        onlyUnpaused
        onlyRole(_FREEZE_MANAGER_ROLE)
        validateAddress(_userAddress)
        onlyWithoutMultiPartition
    {
        _checkUnfreezeAmount(_DEFAULT_PARTITION, _userAddress, _amount);
        _unfreezeTokens(_userAddress, _amount);
        emit TokensUnfrozen(_userAddress, _amount, _DEFAULT_PARTITION);
    }

    /**
     * @notice Gives an account the agent role
     * @notice Granting an agent role allows the account to perform multiple ERC-1400 actions
     * @dev Can only be called by the role admin
     */
    function addAgent(
        address _agent
    ) external onlyRole(_getRoleAdmin(_AGENT_ROLE)) onlyUnpaused {
        _addAgent(_agent);
        emit AgentAdded(_agent);
    }

    /**
     * @notice Revokes an account the agent role
     * @dev Can only be called by the role admin
     */
    function removeAgent(
        address _agent
    ) external onlyRole(_getRoleAdmin(_AGENT_ROLE)) onlyUnpaused {
        _removeAgent(_agent);
        emit AgentRemoved(_agent);
    }

    function recoveryAddress(
        address _lostWallet,
        address _newWallet,
        address _investorOnchainID
    ) external onlyRole(_AGENT_ROLE) returns (bool) {
        emit RecoverySuccess(_lostWallet, _newWallet, _investorOnchainID);
        return _recoveryAddress(_lostWallet, _newWallet);
    }

    /**
     * @notice Retrieves the onchainID address associated with the token.
     */
    function onchainID() external view override returns (address) {
        return _erc3643Storage().onchainID;
    }

    function getFrozenTokens(
        address _userAddress
    ) external view override returns (uint256) {
        return _getFrozenAmountForAdjusted(_userAddress);
    }

    /**
     * @dev Checks if an account has the agent role
     */
    function isAgent(address _agent) external view returns (bool) {
        return _hasRole(_AGENT_ROLE, _agent);
    }

    /**
     * @notice Retrieves the latest version of the contract.
     * @dev The version is represented as a string.
     */
    function version() external view returns (string memory) {
        return Strings.toString(_getLatestVersion());
    }

    /**
     * @notice Retrieves the identity registry contract address.
     */
    function identityRegistry()
        external
        view
        override
        returns (IIdentityRegistry)
    {
        return IIdentityRegistry(_erc3643Storage().identityRegistry);
    }

    /**
     * @notice Retrieves the onchainID address associated with the token.
     */
    function onchainID() external view override returns (address) {
        return _erc3643Storage().onchainID;
    }

    function compliance() external view override returns (ICompliance) {
        return ICompliance(_erc3643Storage().compliance);
    }

    function isAddressRecovered(address _wallet) external view returns (bool) {
        return _isRecovered(_wallet);
    }

    function getStaticResolverKey()
        external
        pure
        override
        returns (bytes32 staticResolverKey_)
    {
        staticResolverKey_ = _ERC3643_RESOLVER_KEY;
    }

    function getStaticFunctionSelectors()
        external
        pure
        override
        returns (bytes4[] memory staticFunctionSelectors_)
    {
        staticFunctionSelectors_ = new bytes4[](19);
        uint256 selectorsIndex;
        staticFunctionSelectors_[selectorsIndex++] = this.burn.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.compliance.selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .forcedTransfer
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .identityRegistry
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this.mint.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.onchainID.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.setOnchainID.selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .setCompliance
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this.setName.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.setSymbol.selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .freezePartialTokens
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .unfreezePartialTokens
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .getFrozenTokens
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .setAddressFrozen
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .setIdentityRegistry
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this.version.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.addAgent.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.removeAgent.selector;
        staticFunctionSelectors_[selectorsIndex++] = this.isAgent.selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .recoveryAddress
            .selector;
        staticFunctionSelectors_[selectorsIndex++] = this
            .isAddressRecovered
            .selector;
    }

    /**
     * @notice Retrieves the identity registry contract address.
     */
    function getStaticInterfaceIds()
        external
        pure
        override
        returns (bytes4[] memory staticInterfaceIds_)
    {
        staticInterfaceIds_ = new bytes4[](1);
        uint256 selectorsIndex;
        staticInterfaceIds_[selectorsIndex++] = type(IERC3643).interfaceId;
    }

    function _checkUnfreezeAmount(
        bytes32 _partition,
        address _userAddress,
        uint256 _amount
    ) private view {
        uint256 frozenAmount = _getFrozenAmountForByPartitionAdjusted(
            _partition,
            _userAddress
        );
        if (frozenAmount < _amount) {
            revert InsufficientFrozenBalance(
                _userAddress,
                _amount,
                frozenAmount,
                _partition
            );
        }
    }
}
