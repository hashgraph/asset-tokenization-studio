/*
                                 Apache License
                           Version 2.0, January 2004
                        http://www.apache.org/licenses/

   TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION

   1. Definitions.

      "License" shall mean the terms and conditions for use, reproduction,
      and distribution as defined by Sections 1 through 9 of this document.

      "Licensor" shall mean the copyright owner or entity authorized by
      the copyright owner that is granting the License.

      "Legal Entity" shall mean the union of the acting entity and all
      other entities that control, are controlled by, or are under common
      control with that entity. For the purposes of this definition,
      "control" means (i) the power, direct or indirect, to cause the
      direction or management of such entity, whether by contract or
      otherwise, or (ii) ownership of fifty percent (50%) or more of the
      outstanding shares, or (iii) beneficial ownership of such entity.

      "You" (or "Your") shall mean an individual or Legal Entity
      exercising permissions granted by this License.

      "Source" form shall mean the preferred form for making modifications,
      including but not limited to software source code, documentation
      source, and configuration files.

      "Object" form shall mean any form resulting from mechanical
      transformation or translation of a Source form, including but
      not limited to compiled object code, generated documentation,
      and conversions to other media types.

      "Work" shall mean the work of authorship, whether in Source or
      Object form, made available under the License, as indicated by a
      copyright notice that is included in or attached to the work
      (an example is provided in the Appendix below).

      "Derivative Works" shall mean any work, whether in Source or Object
      form, that is based on (or derived from) the Work and for which the
      editorial revisions, annotations, elaborations, or other modifications
      represent, as a whole, an original work of authorship. For the purposes
      of this License, Derivative Works shall not include works that remain
      separable from, or merely link (or bind by name) to the interfaces of,
      the Work and Derivative Works thereof.

      "Contribution" shall mean any work of authorship, including
      the original version of the Work and any modifications or additions
      to that Work or Derivative Works thereof, that is intentionally
      submitted to Licensor for inclusion in the Work by the copyright owner
      or by an individual or Legal Entity authorized to submit on behalf of
      the copyright owner. For the purposes of this definition, "submitted"
      means any form of electronic, verbal, or written communication sent
      to the Licensor or its representatives, including but not limited to
      communication on electronic mailing lists, source code control systems,
      and issue tracking systems that are managed by, or on behalf of, the
      Licensor for the purpose of discussing and improving the Work, but
      excluding communication that is conspicuously marked or otherwise
      designated in writing by the copyright owner as "Not a Contribution."

      "Contributor" shall mean Licensor and any individual or Legal Entity
      on behalf of whom a Contribution has been received by Licensor and
      subsequently incorporated within the Work.

   2. Grant of Copyright License. Subject to the terms and conditions of
      this License, each Contributor hereby grants to You a perpetual,
      worldwide, non-exclusive, no-charge, royalty-free, irrevocable
      copyright license to reproduce, prepare Derivative Works of,
      publicly display, publicly perform, sublicense, and distribute the
      Work and such Derivative Works in Source or Object form.

   3. Grant of Patent License. Subject to the terms and conditions of
      this License, each Contributor hereby grants to You a perpetual,
      worldwide, non-exclusive, no-charge, royalty-free, irrevocable
      (except as stated in this section) patent license to make, have made,
      use, offer to sell, sell, import, and otherwise transfer the Work,
      where such license applies only to those patent claims licensable
      by such Contributor that are necessarily infringed by their
      Contribution(s) alone or by combination of their Contribution(s)
      with the Work to which such Contribution(s) was submitted. If You
      institute patent litigation against any entity (including a
      cross-claim or counterclaim in a lawsuit) alleging that the Work
      or a Contribution incorporated within the Work constitutes direct
      or contributory patent infringement, then any patent licenses
      granted to You under this License for that Work shall terminate
      as of the date such litigation is filed.

   4. Redistribution. You may reproduce and distribute copies of the
      Work or Derivative Works thereof in any medium, with or without
      modifications, and in Source or Object form, provided that You
      meet the following conditions:

      (a) You must give any other recipients of the Work or
          Derivative Works a copy of this License; and

      (b) You must cause any modified files to carry prominent notices
          stating that You changed the files; and

      (c) You must retain, in the Source form of any Derivative Works
          that You distribute, all copyright, patent, trademark, and
          attribution notices from the Source form of the Work,
          excluding those notices that do not pertain to any part of
          the Derivative Works; and

      (d) If the Work includes a "NOTICE" text file as part of its
          distribution, then any Derivative Works that You distribute must
          include a readable copy of the attribution notices contained
          within such NOTICE file, excluding those notices that do not
          pertain to any part of the Derivative Works, in at least one
          of the following places: within a NOTICE text file distributed
          as part of the Derivative Works; within the Source form or
          documentation, if provided along with the Derivative Works; or,
          within a display generated by the Derivative Works, if and
          wherever such third-party notices normally appear. The contents
          of the NOTICE file are for informational purposes only and
          do not modify the License. You may add Your own attribution
          notices within Derivative Works that You distribute, alongside
          or as an addendum to the NOTICE text from the Work, provided
          that such additional attribution notices cannot be construed
          as modifying the License.

      You may add Your own copyright statement to Your modifications and
      may provide additional or different license terms and conditions
      for use, reproduction, or distribution of Your modifications, or
      for any such Derivative Works as a whole, provided Your use,
      reproduction, and distribution of the Work otherwise complies with
      the conditions stated in this License.

   5. Submission of Contributions. Unless You explicitly state otherwise,
      any Contribution intentionally submitted for inclusion in the Work
      by You to the Licensor shall be under the terms and conditions of
      this License, without any additional terms or conditions.
      Notwithstanding the above, nothing herein shall supersede or modify
      the terms of any separate license agreement you may have executed
      with Licensor regarding such Contributions.

   6. Trademarks. This License does not grant permission to use the trade
      names, trademarks, service marks, or product names of the Licensor,
      except as required for reasonable and customary use in describing the
      origin of the Work and reproducing the content of the NOTICE file.

   7. Disclaimer of Warranty. Unless required by applicable law or
      agreed to in writing, Licensor provides the Work (and each
      Contributor provides its Contributions) on an "AS IS" BASIS,
      WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
      implied, including, without limitation, any warranties or conditions
      of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
      PARTICULAR PURPOSE. You are solely responsible for determining the
      appropriateness of using or redistributing the Work and assume any
      risks associated with Your exercise of permissions under this License.

   8. Limitation of Liability. In no event and under no legal theory,
      whether in tort (including negligence), contract, or otherwise,
      unless required by applicable law (such as deliberate and grossly
      negligent acts) or agreed to in writing, shall any Contributor be
      liable to You for damages, including any direct, indirect, special,
      incidental, or consequential damages of any character arising as a
      result of this License or out of the use or inability to use the
      Work (including but not limited to damages for loss of goodwill,
      work stoppage, computer failure or malfunction, or any and all
      other commercial damages or losses), even if such Contributor
      has been advised of the possibility of such damages.

   9. Accepting Warranty or Additional Liability. While redistributing
      the Work or Derivative Works thereof, You may choose to offer,
      and charge a fee for, acceptance of support, warranty, indemnity,
      or other liability obligations and/or rights consistent with this
      License. However, in accepting such obligations, You may act only
      on Your own behalf and on Your sole responsibility, not on behalf
      of any other Contributor, and only if You agree to indemnify,
      defend, and hold each Contributor harmless for any liability
      incurred by, or claims asserted against, such Contributor by reason
      of your accepting any such warranty or additional liability.

   END OF TERMS AND CONDITIONS

   APPENDIX: How to apply the Apache License to your work.

      To apply the Apache License to your work, attach the following
      boilerplate notice, with the fields enclosed by brackets "[]"
      replaced with your own identifying information. (Don't include
      the brackets!)  The text should be enclosed in the appropriate
      comment syntax for the file format. We also recommend that a
      file or class name and description of purpose be included on the
      same "printed page" as the copyright notice for easier
      identification within third-party archives.

   Copyright [yyyy] [name of copyright owner]

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.

*/

// SPDX-License-Identifier: MIT
pragma solidity 0.8.18;

import {ZERO_ADDRESS, DEFAULT_PARTITION} from '../../constants/values.sol';
import {_ERC1594_STORAGE_POSITION} from '../../constants/storagePositions.sol';
import {Eip1066} from '../../../layer_0/constants/eip1066.sol';
import {IEip1066} from '../../../layer_1/interfaces/eip1066/IEip1066.sol';
import {LibCommon} from '../../common/libraries/LibCommon.sol';

import {
    IERC1594StorageWrapper
} from '../../../layer_1/interfaces/ERC1400/IERC1594StorageWrapper.sol';
import {IKyc} from '../../../layer_1/interfaces/kyc/IKyc.sol';
import {CapStorageWrapper2} from '../../cap/CapStorageWrapper2.sol';

abstract contract ERC1594StorageWrapper is
    IERC1594StorageWrapper,
    CapStorageWrapper2
{
    struct ERC1594Storage {
        bool issuance;
        bool initialized;
    }

    /**
     * @dev Validation flags structure to control which validations to perform
     */
    struct ValidationFlags {
        bool checkPause; // Check if contract is paused
        bool checkClearing; // Check if clearing is active
        bool checkOperatorRecovery; // Check if operator address is recovered
        bool checkFromRecovery; // Check if from address is recovered
        bool checkToRecovery; // Check if to address is recovered
        bool checkOperatorAccess; // Check if operator has access (whitelist/blacklist)
        bool checkFromAccess; // Check if from address has access
        bool checkToAccess; // Check if to address has access
        bool checkFromKyc; // Check if from address has valid KYC
        bool checkToKyc; // Check if to address has valid KYC
        bool checkBalance; // Check if from address has sufficient balance
    }

    modifier onlyIssuable() {
        _checkIssuable();
        _;
    }

    // solhint-disable-next-line func-name-mixedcase
    function _initialize_ERC1594() internal {
        _erc1594Storage().issuance = true;
        _erc1594Storage().initialized = true;
    }

    /**
     * @notice This function must be called to increase the total supply (Corresponds to mint function of ERC20).
     * @dev It only be called by the token issuer or the operator defined by the issuer. ERC1594 doesn't have
     * have the any logic related to operator but its superset ERC1400 have the operator logic and this function
     * is allowed to call by the operator.
     * @param _tokenHolder The account that will receive the created tokens (account should be whitelisted or KYCed).
     * @param _value The amount of tokens need to be issued
     * @param _data The `bytes calldata _data` allows arbitrary data to be submitted alongside the transfer.
     */
    // TODO: In this case are able to perform that operation another role?
    function _issue(
        address _tokenHolder,
        uint256 _value,
        bytes memory _data
    ) internal {
        // Add a function to validate the `_data` parameter
        _mint(_tokenHolder, _value);
        emit Issued(_msgSender(), _tokenHolder, _value, _data);
    }

    /**
     * @notice This function redeem an amount of the token of a msg.sender. For doing so msg.sender may incentivize
     * using different ways that could be implemented with in the `redeem` function definition. But those
     * implementations are out of the scope of the ERC1594.
     * @param _value The amount of tokens need to be redeemed
     * @param _data The `bytes calldata _data` it can be used in the token contract to authenticate the redemption.
     */
    function _redeem(uint256 _value, bytes calldata _data) internal {
        // Add a function to validate the `_data` parameter
        _burn(_msgSender(), _value);
        emit Redeemed(address(0), _msgSender(), _value, _data);
    }

    /**
     * @notice This function redeem an amount of the token of a msg.sender. For doing so msg.sender may incentivize
     * using different ways that could be implemented with in the `redeem` function definition. But those
     * implementations are out of the scope of the ERC1594.
     * @dev It is analogy to `transferFrom`
     * @param _tokenHolder The account whose tokens gets redeemed.
     * @param _value The amount of tokens need to be redeemed
     * @param _data The `bytes calldata _data` it can be used in the token contract to authenticate the redemption.
     */
    function _redeemFrom(
        address _tokenHolder,
        uint256 _value,
        bytes memory _data
    ) internal {
        // Add a function to validate the `_data` parameter
        _burnFrom(_tokenHolder, _value);
        emit Redeemed(_msgSender(), _tokenHolder, _value, _data);
    }

    // Utility functions moved to LibCommon library

    /**
     * @dev Helper function to validate operations with empty data
     * @param _to The address for validation (can be from or to depending on operation)
     * @param _amount The amount for validation
     * @return isValid Whether the operation is valid
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _validateWithEmptyData(
        address _to,
        uint256 _amount
    )
        internal
        view
        returns (
            bool isValid,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        return _isAbleToTransferMemory(_to, _amount, LibCommon.emptyBytes());
    }

    /**
     * @dev Performs common validation checks based on the provided flags
     * @param _operator The operator address (can be zero address if not applicable)
     * @param _from The from address (can be zero address if not applicable)
     * @param _to The to address (can be zero address if not applicable)
     * @param _amount The amount being transferred/issued/redeemed
     * @param _flags Validation flags indicating which checks to perform
     * @return isValid Whether all enabled validations passed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _performCommonValidation(
        address _operator,
        address _from,
        address _to,
        uint256 _amount,
        ValidationFlags memory _flags
    )
        internal
        view
        returns (
            bool isValid,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Check pause status
        if (_flags.checkPause && _isPaused()) {
            return (
                false,
                Eip1066.PAUSED,
                Eip1066.REASON_EMPTY,
                LibCommon.emptyBytes()
            );
        }

        // Check clearing status
        if (_flags.checkClearing && _isClearingActivated()) {
            return (
                false,
                Eip1066.UNAVAILABLE,
                Eip1066.REASON_CLEARING_IS_ACTIVE,
                LibCommon.emptyBytes()
            );
        }

        // Check operator recovery status
        if (
            _flags.checkOperatorRecovery &&
            _operator != ZERO_ADDRESS &&
            _isRecovered(_operator)
        ) {
            return (
                false,
                Eip1066.REVOKED_OR_BANNED,
                IEip1066.ReasonAddressRecovered.selector,
                abi.encode(_operator)
            );
        }

        // Check from address recovery status
        if (
            _flags.checkFromRecovery &&
            _from != ZERO_ADDRESS &&
            _isRecovered(_from)
        ) {
            return (
                false,
                Eip1066.REVOKED_OR_BANNED,
                IEip1066.ReasonAddressRecovered.selector,
                abi.encode(_from)
            );
        }

        // Check to address recovery status
        if (
            _flags.checkToRecovery && _to != ZERO_ADDRESS && _isRecovered(_to)
        ) {
            return (
                false,
                Eip1066.REVOKED_OR_BANNED,
                IEip1066.ReasonAddressRecovered.selector,
                abi.encode(_to)
            );
        }

        // Check operator access (whitelist/blacklist)
        if (
            _flags.checkOperatorAccess &&
            _operator != ZERO_ADDRESS &&
            !_isAbleToAccess(_operator)
        ) {
            return (
                false,
                Eip1066.DISALLOWED_OR_STOP,
                IEip1066.ReasonAddressInBlacklistOrNotInWhitelist.selector,
                abi.encode(_operator)
            );
        }

        // Check from address access (whitelist/blacklist)
        if (
            _flags.checkFromAccess &&
            _from != ZERO_ADDRESS &&
            !_isAbleToAccess(_from)
        ) {
            return (
                false,
                Eip1066.DISALLOWED_OR_STOP,
                IEip1066.ReasonAddressInBlacklistOrNotInWhitelist.selector,
                abi.encode(_from)
            );
        }

        // Check to address access (whitelist/blacklist)
        if (
            _flags.checkToAccess && _to != ZERO_ADDRESS && !_isAbleToAccess(_to)
        ) {
            return (
                false,
                Eip1066.DISALLOWED_OR_STOP,
                IEip1066.ReasonAddressInBlacklistOrNotInWhitelist.selector,
                abi.encode(_to)
            );
        }

        // Check from address KYC status
        if (
            _flags.checkFromKyc &&
            _from != ZERO_ADDRESS &&
            !_verifyKycStatus(IKyc.KycStatus.GRANTED, _from)
        ) {
            return (
                false,
                Eip1066.DISALLOWED_OR_STOP,
                IEip1066.ReasonKycNotGranted.selector,
                abi.encode(_from)
            );
        }

        // Check to address KYC status
        if (
            _flags.checkToKyc &&
            _to != ZERO_ADDRESS &&
            !_verifyKycStatus(IKyc.KycStatus.GRANTED, _to)
        ) {
            return (
                false,
                Eip1066.DISALLOWED_OR_STOP,
                IEip1066.ReasonKycNotGranted.selector,
                abi.encode(_to)
            );
        }

        // Check balance (only if from address is provided and amount > 0)
        if (_flags.checkBalance && _from != ZERO_ADDRESS && _amount > 0) {
            uint256 fromBalance = _balanceOfAdjusted(_from);
            if (fromBalance < _amount) {
                return (
                    false,
                    Eip1066.INSUFFICIENT_FUNDS,
                    IEip1066.ReasonInsufficientBalance.selector,
                    abi.encode(_from, fromBalance, _amount, DEFAULT_PARTITION)
                );
            }
        }

        // All validations passed
        return (true, Eip1066.SUCCESS, bytes32(0), LibCommon.emptyBytes());
    }

    /**
     * @notice A security token issuer can specify that issuance has finished for the token
     * (i.e. no new tokens can be minted or issued).
     * @dev If a token returns FALSE for `isIssuable()` then it MUST always return FALSE in the future.
     * If a token returns FALSE for `isIssuable()` then it MUST never allow additional tokens to be issued.
     * @return bool `true` signifies the minting is allowed. While `false` denotes the end of minting
     */
    function _isIssuable() internal view returns (bool) {
        return _erc1594Storage().issuance;
    }

    function _isAbleToTransfer(
        address _to,
        uint256 _value,
        bytes calldata _data
    )
        internal
        view
        returns (
            bool isAbleToTransfer,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Convert calldata to memory for consistent handling
        return _isAbleToTransferMemory(_to, _value, LibCommon.toMemory(_data));
    }

    /**
     * @dev Validates if a transferFrom operation can be performed
     * @param _from The address to transfer from
     * @param _to The address to transfer to
     * @param _value The amount to transfer
     * @return isAbleToTransfer Whether the transfer is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToTransferFrom(
        address _from,
        address _to,
        uint256 _value,
        bytes calldata _data
    )
        internal
        view
        returns (
            bool isAbleToTransfer,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Convert calldata to memory for consistent handling
        return
            _isAbleToTransferFromMemory(
                _from,
                _to,
                _value,
                LibCommon.toMemory(_data)
            );
    }

    /**
     * @dev Validates if an issue operation can be performed
     * @param _to The address to issue tokens to
     * @param _value The amount to issue
     * @return isAbleToIssue Whether the issuance is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToIssue(
        address _to,
        uint256 _value,
        bytes calldata _data
    )
        internal
        view
        returns (
            bool isAbleToIssue,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Convert calldata to memory for consistent handling
        return _isAbleToIssueMemory(_to, _value, LibCommon.toMemory(_data));
    }

    /**
     * @dev Memory version of _isAbleToIssue for bytes memory compatibility
     * @param _to The address to issue tokens to
     * @param _value The amount to issue
     * _data The data parameter as bytes memory
     * @return isAbleToIssue Whether the issuance is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToIssueMemory(
        address _to,
        uint256 _value,
        bytes memory /* _data */
    )
        internal
        view
        returns (
            bool isAbleToIssue,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Check if issuance is still allowed (operation-specific validation)
        if (!_isIssuable()) {
            return (
                false,
                Eip1066.DISALLOWED_OR_STOP,
                Eip1066.REASON_ISSUANCE_CLOSED,
                LibCommon.emptyBytes()
            );
        }

        // Check for zero address (operation-specific validation)
        if (_to == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }

        // Use common validation layer for shared checks
        ValidationFlags memory flags = _createIssueValidationFlags();
        (
            isAbleToIssue,
            statusCode,
            reasonCode,
            details
        ) = _performCommonValidation(
            ZERO_ADDRESS, // No operator for issue
            ZERO_ADDRESS, // No from address for issue
            _to, // To address
            _value, // Amount
            flags // Validation flags
        );

        // If common validation failed, return the error
        if (!isAbleToIssue) {
            return (isAbleToIssue, statusCode, reasonCode, details);
        }

        // All validations passed
        return (true, Eip1066.SUCCESS, bytes32(0), LibCommon.emptyBytes());
    }

    /**
     * @dev Validates if a redeem operation can be performed
     * @param _from The address to redeem tokens from
     * @param _value The amount to redeem
     * @return isAbleToRedeem Whether the redemption is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToRedeem(
        address _from,
        uint256 _value,
        bytes calldata _data
    )
        internal
        view
        returns (
            bool isAbleToRedeem,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Convert calldata to memory for consistent handling
        return _isAbleToRedeemMemory(_from, _value, LibCommon.toMemory(_data));
    }

    /**
     * @dev Memory version of _isAbleToRedeem for bytes memory compatibility
     * @param _from The address to redeem tokens from
     * @param _value The amount to redeem
     * _data The data parameter as bytes memory
     * @return isAbleToRedeem Whether the redemption is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToRedeemMemory(
        address _from,
        uint256 _value,
        bytes memory /* _data */
    )
        internal
        view
        returns (
            bool isAbleToRedeem,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Check for zero address (operation-specific validation)
        if (_from == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }

        // Use common validation layer for shared checks
        ValidationFlags memory flags = _createRedeemValidationFlags();
        (
            isAbleToRedeem,
            statusCode,
            reasonCode,
            details
        ) = _performCommonValidation(
            ZERO_ADDRESS, // No operator for redeem
            _from, // From address
            ZERO_ADDRESS, // No to address for redeem
            _value, // Amount
            flags // Validation flags
        );

        // If common validation failed, return the error
        if (!isAbleToRedeem) {
            return (isAbleToRedeem, statusCode, reasonCode, details);
        }

        // All validations passed
        return (true, Eip1066.SUCCESS, bytes32(0), LibCommon.emptyBytes());
    }

    /**
     * @dev Validates if an approve operation can be performed
     * @param _owner The address that owns the tokens
     * @param _spender The address that will be approved to spend tokens
     *        _amount The amount to approve (not used in validation but kept for interface compatibility)
     * @return isAbleToApprove Whether the approval is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToApprove(
        address _owner,
        address _spender,
        uint256 _amount
    )
        internal
        view
        returns (
            bool isAbleToApprove,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        // Check for zero addresses (operation-specific validation)
        if (_owner == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }
        if (_spender == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }

        // Use common validation layer for shared checks
        ValidationFlags memory flags = _createApproveValidationFlags();
        (
            isAbleToApprove,
            statusCode,
            reasonCode,
            details
        ) = _performCommonValidation(
            _spender, // Spender as operator
            _owner, // Owner as from address
            ZERO_ADDRESS, // No to address for approve
            _amount, // Amount (not used for validation)
            flags // Validation flags
        );

        // If common validation failed, return the error
        if (!isAbleToApprove) {
            return (isAbleToApprove, statusCode, reasonCode, details);
        }

        // All validations passed
        return (true, Eip1066.SUCCESS, bytes32(0), LibCommon.emptyBytes());
    }

    function _checkIssuable() internal view {
        if (!_isIssuable()) {
            revert IEip1066.ExtendedError(
                Eip1066.DISALLOWED_OR_STOP,
                IEip1066.ReasonIssuanceClosed.selector,
                LibCommon.emptyBytes()
            );
        }
    }

    /**
     * @notice Determines whether the token can be transferred.
     * @param _to The address to which the tokens will be transferred.
     * @param _value The amount of tokens to be transferred.
     * @param _data Additional data attached to the transfer.
     * @return canTransfer Whether the transfer is allowed.
     * @return statusCode EIP1066 status code.
     * @return reasonCode Reason code for the status.
     */
    function _canTransfer(
        address _to,
        uint256 _value,
        bytes memory _data
    )
        internal
        view
        returns (bool canTransfer, bytes1 statusCode, bytes32 reasonCode)
    {
        (canTransfer, statusCode, reasonCode, ) = _isAbleToTransferMemory(
            _to,
            _value,
            _data
        );
        return (canTransfer, statusCode, reasonCode);
    }

    /**
     * @dev Memory version of _isAbleToTransfer for bytes memory compatibility
     * @param _to The address to transfer to
     * @param _value The amount to transfer
     * _data The data parameter as bytes memory
     * @return isAbleToTransfer Whether the transfer is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToTransferMemory(
        address _to,
        uint256 _value,
        bytes memory /* _data */
    )
        internal
        view
        returns (
            bool isAbleToTransfer,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        address sender = _msgSender();

        // Check for zero address first (operation-specific validation)
        if (_to == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }

        // Use common validation layer for shared checks
        ValidationFlags memory flags = _createTransferValidationFlags();
        (
            isAbleToTransfer,
            statusCode,
            reasonCode,
            details
        ) = _performCommonValidation(
            ZERO_ADDRESS, // No operator for direct transfer
            sender, // From address
            _to, // To address
            _value, // Amount
            flags // Validation flags
        );

        // If common validation failed, return the error
        if (!isAbleToTransfer) {
            return (isAbleToTransfer, statusCode, reasonCode, details);
        }

        // All validations passed
        return (true, Eip1066.SUCCESS, bytes32(0), LibCommon.emptyBytes());
    }

    /**
     * @notice Determines whether tokens can be transferred from one address to another.
     * @param _from The address from which the tokens will be transferred.
     * @param _to The address to which the tokens will be transferred.
     * @param _value The amount of tokens to be transferred.
     * @param _data Additional data attached to the transfer.
     * @return canTransfer Whether the transfer is allowed.
     * @return statusCode EIP1066 status code.
     * @return reasonCode Reason code for the status.
     */
    function _canTransferFrom(
        address _from,
        address _to,
        uint256 _value,
        bytes memory _data
    )
        internal
        view
        returns (bool canTransfer, bytes1 statusCode, bytes32 reasonCode)
    {
        (canTransfer, statusCode, reasonCode, ) = _isAbleToTransferFromMemory(
            _from,
            _to,
            _value,
            _data
        );
        return (canTransfer, statusCode, reasonCode);
    }

    /**
     * @dev Memory version of _isAbleToTransferFrom for bytes memory compatibility
     * @param _from The address to transfer from
     * @param _to The address to transfer to
     * @param _value The amount to transfer
     * _data The data parameter as bytes memory
     * @return isAbleToTransfer Whether the transfer is allowed
     * @return statusCode EIP1066 status code
     * @return reasonCode EIP1066 reason code
     * @return details ABI-encoded error details
     */
    function _isAbleToTransferFromMemory(
        address _from,
        address _to,
        uint256 _value,
        bytes memory /*_data*/
    )
        internal
        view
        returns (
            bool isAbleToTransfer,
            bytes1 statusCode,
            bytes32 reasonCode,
            bytes memory details
        )
    {
        address operator = _msgSender();

        // Check for zero addresses first (operation-specific validation)
        if (_from == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }
        if (_to == ZERO_ADDRESS) {
            return (
                false,
                Eip1066.NOT_FOUND_UNEQUAL_OR_OUT_OF_RANGE,
                Eip1066.REASON_INVALID_ZERO_ADDRESS,
                LibCommon.emptyBytes()
            );
        }

        // Check allowance first (operation-specific validation)
        if (_from != operator) {
            uint256 currentAllowance = _allowanceAdjusted(_from, operator);
            if (currentAllowance < _value) {
                return (
                    false,
                    Eip1066.INSUFFICIENT_FUNDS,
                    Eip1066.REASON_EMPTY,
                    abi.encode(_from, operator, currentAllowance, _value)
                );
            }
        }

        // Use common validation layer for shared checks
        ValidationFlags memory flags = _createTransferFromValidationFlags();
        (
            isAbleToTransfer,
            statusCode,
            reasonCode,
            details
        ) = _performCommonValidation(
            operator, // Operator address
            _from, // From address
            _to, // To address
            _value, // Amount
            flags // Validation flags
        );

        // If common validation failed, return the error
        if (!isAbleToTransfer) {
            return (isAbleToTransfer, statusCode, reasonCode, details);
        }

        // All validations passed
        return (true, Eip1066.SUCCESS, bytes32(0), LibCommon.emptyBytes());
    }

    /**
     * @dev Creates validation flags for transfer operations
     */
    function _createTransferValidationFlags()
        internal
        pure
        returns (ValidationFlags memory)
    {
        return
            ValidationFlags({
                checkPause: true,
                checkClearing: true,
                checkOperatorRecovery: false,
                checkFromRecovery: true,
                checkToRecovery: true,
                checkOperatorAccess: false,
                checkFromAccess: true,
                checkToAccess: true,
                checkFromKyc: true,
                checkToKyc: true,
                checkBalance: true
            });
    }

    /**
     * @dev Creates validation flags for transferFrom operations
     */
    function _createTransferFromValidationFlags()
        internal
        pure
        returns (ValidationFlags memory)
    {
        return
            ValidationFlags({
                checkPause: true,
                checkClearing: true,
                checkOperatorRecovery: true,
                checkFromRecovery: true,
                checkToRecovery: true,
                checkOperatorAccess: true,
                checkFromAccess: true,
                checkToAccess: true,
                checkFromKyc: true,
                checkToKyc: true,
                checkBalance: true
            });
    }

    /**
     * @dev Creates validation flags for issue operations
     */
    function _createIssueValidationFlags()
        internal
        pure
        returns (ValidationFlags memory)
    {
        return
            ValidationFlags({
                checkPause: true,
                checkClearing: true,
                checkOperatorRecovery: false,
                checkFromRecovery: false,
                checkToRecovery: true,
                checkOperatorAccess: false,
                checkFromAccess: false,
                checkToAccess: true,
                checkFromKyc: false,
                checkToKyc: true,
                checkBalance: false
            });
    }

    /**
     * @dev Creates validation flags for redeem operations
     */
    function _createRedeemValidationFlags()
        internal
        pure
        returns (ValidationFlags memory)
    {
        return
            ValidationFlags({
                checkPause: true,
                checkClearing: true,
                checkOperatorRecovery: false,
                checkFromRecovery: true,
                checkToRecovery: false,
                checkOperatorAccess: false,
                checkFromAccess: true,
                checkToAccess: false,
                checkFromKyc: true,
                checkToKyc: false,
                checkBalance: true
            });
    }

    /**
     * @dev Creates validation flags for approve operations
     */
    function _createApproveValidationFlags()
        internal
        pure
        returns (ValidationFlags memory)
    {
        return
            ValidationFlags({
                checkPause: true,
                checkClearing: false,
                checkOperatorRecovery: true,
                checkFromRecovery: true,
                checkToRecovery: false,
                checkOperatorAccess: true,
                checkFromAccess: true,
                checkToAccess: false,
                checkFromKyc: false,
                checkToKyc: false,
                checkBalance: false
            });
    }

    function _erc1594Storage()
        internal
        pure
        returns (ERC1594Storage storage ds)
    {
        bytes32 position = _ERC1594_STORAGE_POSITION;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            ds.slot := position
        }
    }
}
